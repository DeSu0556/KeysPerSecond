plugins{
	id 'java'
	id 'application'
	id 'eclipse'
	id 'com.github.johnrengelman.shadow' version '7.1.2'
	id 'edu.sc.seis.launch4j' version '2.5.1'
}

sourceSets{
	main{
		java.srcDirs = ['src']
		resources.srcDirs = ['resources']
	}
}

dependencies{
	implementation 'dev.roanh.util:util:2.4'
	implementation 'com.github.kwhat:jnativehook:2.2.1'
}

repositories{
	//Local cache
	if(project.hasProperty("nexusPublic")){
		maven{
			allowInsecureProtocol = true
			url "$nexusPublic"
		}
	}
	mavenCentral();
}

version = 'v1.0'
shadowJar.archiveName = 'KeysPerSecond-' + version + '.jar'
eclipse.classpath.downloadSources = true
eclipse.classpath.downloadJavadoc = true
compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8
javadoc.options.memberLevel = JavadocMemberLevel.PRIVATE
application.mainClassName = 'dev.roanh.kps.Main'

launch4j{
	jarTask = project.tasks.shadowJar
	mainClassName = application.mainClassName
	icon = "${projectDir}/kps.ico"
	jreMinVersion = project.sourceCompatibility
	bundledJrePath = "%JAVA_HOME%"
	outfile = project.name + '-' + project.version + '.exe'
	fileDescription = project.name
	version = project.version.substring(1)
	textVersion = project.version.substring(1)
	copyright = "Roan Hofland"
}
